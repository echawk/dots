;; -*- mode: lisp -*-

(require 'asdf)
(asdf:load-system :uiop)

;; Muffle warnings while we load our init.
#+SBCL (declaim (sb-ext:muffle-conditions style-warning))

;; Switch back to cl-user package.
(in-package :cl-user)

(defmacro with-temporary-directory (form)
  "Evaluate form within a temporary directory created by `mktemp -d`."
  `(let ((temp-dir
           (string-trim '(#\Newline)
                        (uiop:run-program "mktemp -d" :output :string))))
     (uiop:with-current-directory (temp-dir)
       ,form)
     (uiop:run-program (concatenate 'string "rm -vrf " temp-dir))))

;;; Install quicklisp if it is not already installed.
#-QUICKLISP
(let ((quicklisp-init (merge-pathnames "quicklisp/setup.lisp"
                                       (user-homedir-pathname))))
  (unless (probe-file quicklisp-init)
    (with-temporary-directory
        (progn
          (uiop:run-program "curl -O https://beta.quicklisp.org/quicklisp.lisp"
                            :output *standard-output*)
          (load "quicklisp.lisp")
          (uiop:symbol-call :quicklisp-quickstart '#:install))))

  (when (probe-file quicklisp-init)
    (load quicklisp-init))

  ;; Also install the ultralisp dist while we are at it...
  (unless (uiop:symbol-call :ql-dist '#:find-dist "ultralisp")
    (uiop:symbol-call :ql-dist '#:install-dist "http://dist.ultralisp.org/" :prompt nil)))

;; how to get the current directory always detectable by quicklisp
#+quicklisp
(push '*default-pathname-defaults* asdf:*central-registry*)

;; TODO: figure out how to incorporate ql-https
#+(and QUICKLISP DARWIN)
(progn
  (defun install-ql-https ()
    (uiop:with-current-directory ((car ql:*local-project-directories*))
      (uiop:run-program
       (concatenate
          'string
          "sh -c '"
          "HOME=/dev/null "
          "git clone --depth 1 "
          "https://github.com/rudolfochrist/ql-https"
          "'"))))
  (defun setup-ql-https ()
    (unless (ql:where-is-system :ql-https)
      (install-ql-https))
    (progn
      (load (merge-pathnames (ql:where-is-system :ql-https) "ql-setup.lisp"))
      (ql:quickload :ql-https)
      (quicklisp:setup)

      (let ((sym (uiop:find-symbol* "*QUIETLY-USE-HTTPS*" :ql-https)))
        (setf (symbol-value sym) t))))

  ;; (setup-ql-https)
  )

;; https://github.com/fukamachi/qlot
;; https://www.clpm.dev/
;; https://medium.com/@MartinCracauer/static-type-checking-in-the-programmable-programming-language-lisp-79bb79eb068a

#+SBCL
(defun install-ocicl-cmd ()
  (with-temporary-directory
      (progn
        (uiop:run-program
         (concatenate
          'string
          "sh -c '"
          "HOME=/dev/null "
          "git clone --depth 1 "
          "https://github.com/ocicl/ocicl"
          "'"))
        (uiop:with-current-directory
            ((merge-pathnames "ocicl" (uiop:getcwd)))
          ;; FIXME: try to get the fork version to work.
          ;;(ql:quickload '(:sb-posix))
          ;; (when (zerop (sb-posix:fork))
          ;;   (defconstant +dynamic-space-size+ 2048)
          ;;   (load "setup.lisp"))
          (uiop:run-program
           (concatenate
            'string
            "sh -c '"
            "sbcl --eval \"(defconstant +dynamic-space-size+ 2048)\" --load setup.lisp"
            "'"))))))

(defun load-ocicl-runtime-file (ocicl-runtime-file)
  (if (probe-file ocicl-runtime-file)
      (load ocicl-runtime-file)
      (unless
          (uiop:file-exists-p
           (merge-pathnames ".local/bin/ocicl" (user-homedir-pathname)))
        (install-ocicl-cmd)
        (uiop:run-program "ocicl setup")
        (load ocicl-runtime-file))))

;; Setup OCICL if we have the binary...
(when (uiop:file-exists-p
       (merge-pathnames ".local/bin/ocicl" (user-homedir-pathname)))
  #-OCICL
  (let ((ocicl-runtime-file
          (merge-pathnames ".local/share/ocicl/ocicl-runtime.lisp"
                           (user-homedir-pathname))))
    (load-ocicl-runtime-file ocicl-runtime-file)
    #+OCICL (asdf:initialize-source-registry
             (list :source-registry (list :directory (uiop:getcwd))
                   :inherit-configuration))))

;; Thank you : https://gist.github.com/ryukinix/5273af4b25dec53ed9f078bd7e350d88
(defun executables ()
  (loop with path = (uiop:getenv "PATH")
        for p in (uiop:split-string path :separator ":")
        for dir = (probe-file p)
        when (uiop:directory-exists-p dir)
          append (uiop:directory-files dir)))

(defun find-executable-p (name)
  (find name (executables)
        :test #'equalp
        :key #'pathname-name))

;; Make this dummy package to allow for the below code snippet to work.
(defpackage :qlot/cli (:export :main))

#+(and QUICKLISP SBCL)
(defun make-qlot-exe ()
  (unless (or
           (uiop:find-package* :slynk)
           (uiop:find-package* :slime)
           (and (uiop:find-package* :bordeaux-threads)
                (> (length (uiop:symbol-call :bordeaux-threads '#:all-threads)) 1)))
    (ql:quickload :sb-posix)
    (when (zerop (sb-posix:fork))
      (ql:quickload '(:uiop :qlot :qlot/cli))
      (setq uiop:*image-entry-point* #'qlot/cli:main)
      (uiop:dump-image "qlot" :executable t :compression t))))

;; Install `qlot` in the REPL if it isn't available in $PATH.
#+(and QUICKLISP SBCL)
(unless (find-executable-p "qlot")
  (format t "NOTE: There is no `qlot` executable on this system. Installing via quicklisp...")
  (uiop:with-current-directory
      ((merge-pathnames ".local/bin" (user-homedir-pathname)))
    (make-qlot-exe)))
;; (push ".qlot" asdf::*default-source-registry-exclusions*)

#+(and QUICKLISP SBCL)
(defun update-all-dists ()
  ;; FIXME: I don't think this works?
  (let ((*standard-input*  (make-string-input-stream "\n"))
        (*standard-output* (make-broadcast-stream)))
    (ql:update-all-dists)))

;; Unmuffle our warnings again.
#+SBCL (declaim (sb-ext:unmuffle-conditions style-warning))
